plugins {
    id 'fabric-loom' version '0.2.1-SNAPSHOT'
    id 'org.jetbrains.kotlin.jvm' version '1.3.30'
    id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

archivesBaseName = "Polyester"
version = project.mod_version

repositories {
    mavenCentral()
}

minecraft {
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}"
    modCompile "net.fabricmc:fabric-loader:${project.loader_version}"

    // Fabric API. This is technically optional, but you probably want it anyway.
    modCompile "net.fabricmc:fabric:${project.fabric_version}"
    modCompile "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
    compileOnly "net.fabricmc:fabric-language-kotlin:${project.fabric_kotlin_version}"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

// Modified from the Cotton buildscript (https://github.com/CottonMC/Cotton-ECS)
publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifactId = archivesBaseName

            pom.withXml {
                def pomFile = asNode()
                def depsNode = pomFile.get("dependencies")
                pomFile.remove(depsNode)

                def newDeps = pomFile.appendNode("dependencies")
                configurations.modCompile.getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                    def artifactNode = newDeps.appendNode("dependency")
                    artifactNode.appendNode('groupId', it.moduleGroup)
                    artifactNode.appendNode('artifactId', it.moduleName)
                    artifactNode.appendNode('version', it.moduleVersion)
                }
            }
            artifacts = [
                    artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar") { //release jar - file location not provided anywhere in loom
                        classifier null
                        builtBy remapJar
                    }/*,
                    artifact ("${project.buildDir.absolutePath}/libs/${archivesBaseName}-${project.version}.jar") { //release jar - file location not provided anywhere in loom
                        classifier "dev"
                        builtBy remapJar
                    },
                    artifact (tasks.sourcesJar)*/
            ]
        }
    }
}
